---
layout: page
title: "PC-SIG Diskette Library (Disk #128)"
permalink: /software/pcx86/sw/misc/pcsig/0001-0999/DISK0128/
machines:
  - id: ibm5170
    type: pcx86
    config: /machines/pcx86/ibm/5170/cga/1024kb/rev3/machine.xml
    diskettes: /machines/pcx86/diskettes.json,/disks/pcsigdisks/pcx86/diskettes.json
    autoGen: true
    autoMount:
      B: "PC-SIG Library Disk #0128"
    autoType: $date\r$time\rB:\rDIR\r
---

{% include machine.html id="ibm5170" %}

{% comment %}info_begin{% endcomment %}

## Information about "DBASE II UTIL"

    dBase and BASIC programs.
{% comment %}info_end{% endcomment %}

{% comment %}samples_begin{% endcomment %}

## ANIMAL.BAS

{% raw %}
```bas
10 CLS
100 PRINT "PLAY 'GUESS THE ANIMAL' WITH THE COMPUTER"
150 PRINT "THINK OF AN ANIMAL AND THE COMPUTER WILL TRY TO GUESS IT"
200 PRINT :PRINT "VALID COMMANDS ARE 'LIST' , 'SAVE' AND 'END'":PRINT
300 PRINT:PRINT "Think of an animal.  The computer will ask you some questions and try to guess  what animal it is. If the computer doesn't know what the animal is it will ask  you and it will remember that animal from then on."
400 PRINT:PRINT:PRINT"Whenever you are asked if you are thinking of an animal, you can type  'yes'    (or 'y') to have the computer guess an animal,or 'list' to have the computer    list the animals it knows,or 'end' to end the game (without save animal f
450 PRINT:PRINT:PRINT"█ Warning █ !!! BE SURE THE CAPS LOCK KEY IS ON!!!"
500 DIM A$(200)
515 PRINT:PRINT "ANIMAL FILES ON RECORD:"
520 ON ERROR GOTO 10000:PRINT :FILES "*.GME"
522 PRINT:PRINT"You can use an existing animal file as listed above or you can give it a new    name to create a new file"
525 PRINT :INPUT "ENTER THE NAME OF THE ANIMAL FILE (WITHOUT '.GME'";F$:F$=F$+".GME"
527 IF ASC(F$)>96 THEN PRINT "put caps lock key on":GOTO 525
530 ON ERROR GOTO 700
550 OPEN F$ FOR INPUT AS 1:INPUT #1,N:FOR I=1 TO N:INPUT #1,A$(I):NEXT:A$(0)=STR$(N):CLOSE:ON ERROR GOTO 0:GOTO 1300
700 ON ERROR GOTO 1050:RESUME 550
1050 FOR I=0 TO 3:READ A$(I):NEXT
1100 DATA "4","\QDOES IT SWIM\Y2\N3\","\ASHARK","\AEAGLE"
1300 INPUT "ARE YOU THINKING OF AN ANIMAL   (or save,end,list)";Z9$:IF LEFT$(Z9$,1)="Y" THEN 1350 ELSE IF LEFT$(Z9$,1)="N" THEN 1300
1310 IF Z9$="SAVE" THEN OPEN F$ FOR OUTPUT AS 1:FOR I = 0 TO VAL(A$(0)):PRINT #1,A$(I):NEXT:PRINT #1,CHR$(26):CLOSE:RESET:GOTO 1300
1315 IF Z9$="END" THEN CLOSE:RESET:PRINT "THANK YOU FOR PLAYING":GOTO 9999
1320 IF Z9$="LIST" THEN PRINT "THE ANIMALS I ALREADY KNOW ARE:":FOR I=1 TO 200:F=INSTR(1,A$(I),"\A"):IF F<>0 THEN PRINT MID$(A$(I),F+2)
1325 IF Z9$="LIST" THEN NEXT:GOTO 1300
1350 K=1
1400 Q$=A$(K):E=-1:GOSUB 3000:IF LEN(A$(K))=0 THEN E=0:GOTO 3000 ELSE IF LEFT$(A$(K),2)="\Q" THEN 1400 ELSE F=INSTR(1,A$(K),"\A"):PRINT "IS IT A "MID$(A$(K),F+2):INPUT Z7$:Z7$=LEFT$(Z7$,1)
1450 IF Z7$="Y" THEN PRINT "WHY NOT TRY ANOTHER ANIMAL":GOTO 1300
2000 INPUT "THE ANIMAL YOU WERE THINKING OF WAS A ";Z9$
2050 F=INSTR(1,A$(K),"\A"):PRINT "PLEASE TYPE IN A QUESTION THAT WOULD DISTINGUISH A "Z9$" FROM A "MID$(A$(K),F+2):INPUT Z8$
2100 PRINT "FOR A " Z9$" THE ANSWER WOULD BE";:INPUT Z7$:Z7$=LEFT$(Z7$,1):IF Z7$="Y" THEN Z6$="N" ELSE IF Z7$="N" THEN Z6$="Y" ELSE PRINT "PLEASE ANSWER 'YES' OR 'NO'":GOTO 2100
2200 Z1=VAL(A$(0)):A$(0)=STR$(Z1+2):A$(Z1)=A$(K):A$(Z1+1)="\A"+Z9$:A$(K)="\Q"+Z8$+"\"+Z7$+STR$(Z1+1)+"\"+Z6$+STR$(Z1)+"\"
2300 GOTO 1300
3000 PRINT MID$(Q$,3,INSTR(3,Q$,"\")-3);:INPUT Z9$:Z9$=LEFT$(Z9$,1):IF Z9$<>"Y" THEN Z9$="N"
3100 Z1=INSTR(3,Q$,"\"+Z9$)+2:Z2=INSTR(Z1,Q$,"\"):K=VAL(MID$(Q$,Z1,Z2-Z1)):IF E THEN RETURN
9999 END
10000 PRINT "NO ANIMAL FILES":RESUME 522
```
{% endraw %}

## BANNER.BAS

{% raw %}
```bas
20 PRINT "WARNING !!!!! THE CAPS-LOCK KEY MUST BE ON !!! "
30 PRINT "-----------------------------------------------"
50 INPUT "horizontal size ";X
60 INPUT "vertical size ";Y
70 INPUT "centered (y/n)";L$
75 IF ASC(L$)>96 THEN PRINT "put caps lock key on":GOTO 70
80 G1=0: IF LEFT$(L$,1)="Y" THEN G1=1
90 PRINT "letter or word to make large letters from or 'ALL' to make the large letters out of same smaller one":INPUT M$
110 INPUT "statement to print";A$
120 INPUT; "set the page and press return to print";O$
140 FOR T= 1 TO LEN(A$)
150 P$=MID$(A$,T,1)
160 FOR O=1 TO 50
170 READ S$,S(1),S(2),S(3),S(4),S(5),S(6),S(7)
180 REM if p$=" " then 500
190 IF P$=S$ THEN 210
200 NEXT O
210 RESTORE
220 X$=M$
230 IF M$="ALL" THEN X$=S$
240 FOR U=1 TO 7
250 FOR K= 8 TO 0 STEP -1
260 IF 2^K<S(U) THEN 290
270 J(9-K)=0
280 GOTO 310
290 J(9-K)=1:S(U)=S(U)-2^K
300 IF S(U)=1 THEN 520
310 NEXT K
320 FOR T1=1 TO X
330 PRINT TAB((63-4.5*Y)*G1/(LEN(X$))+1);
340 FOR B=1 TO F(U)
350 IF J(B)=0 THEN 380
360 FOR I=1 TO Y:PRINT X$;:NEXT I
370 GOTO 420
380 FOR I=1 TO Y
390 FOR I1 =1 TO LEN(X$)
400 PRINT " ";:NEXT I1
410 NEXT I
420 NEXT B
430 PRINT " "
440 NEXT T1
450 NEXT U
460 FOR H=1 TO 2*X:PRINT " ":NEXT H
470 NEXT T
480 FOR H=1 TO 75:PRINT " ":NEXT H
490 END
500 FOR H=1 TO 7*X:PRINT " ";:NEXT H
510 GOTO 470
520 F(U)=9-K:GOTO 320
530 DATA " ",000,000,000,000,000,000,000
540 DATA "A",505,037,035,034,035,037,505
550 DATA "B",512,274,274,274,274,274,239
560 DATA "C",125,131,258,258,258,131,069
570 DATA "D",512,258,258,258,258,131,125
580 DATA "E",512,274,274,274,274,258,258
590 DATA "F",512,018,018,018,018,002,002
600 DATA "G",125,131,258,258,290,163,101
610 DATA "H",512,017,017,017,017,017,512
620 DATA "I",258,258,258,512,258,258,258
630 DATA "J",065,129,257,257,257,129,128
635 DATA "K",512,017,017,041,069,131,259
640 DATA "L",512,257,257,257,257,257,257
650 DATA "M",512,007,013,125,013,007,512
660 DATA "N",512,007,009,017,033,193,512
670 DATA "O",125,131,258,258,258,131,125
680 DATA "P",512,018,018,018,018,018,015
690 DATA "Q",125,131,258,258,322,131,381
700 DATA "R",512,018,018,050,082,146,271
710 DATA "S",069,139,274,274,274,163,069
720 DATA "T",002,002,002,512,002,002,002
730 DATA "U",128,129,257,257,257,129,128
740 DATA "V",064,065,129,257,129,065,064
750 DATA "W",256,257,129,065,129,257,256
760 DATA "X",388,069,041,017,041,069,388
770 DATA "Y",008,009,017,481,017,009,008
780 DATA "Z",386,322,290,274,266,262,260
790 DATA "0",125,131,262,274,322,131,125
800 DATA "1",000,261,259,512,257,257,000
810 DATA "2",261,387,322,290,274,267,261
820 DATA "3",066,130,258,274,266,150,100
830 DATA "4",033,049,041,037,035,512,033
840 DATA "5",160,274,274,274,274,274,226
850 DATA "6",194,291,293,297,305,289,193
860 DATA "7",258,130,066,034,018,010,008
870 DATA "8",069,171,274,274,274,171,069
880 DATA "9",263,138,074,042,026,010,007
890 DATA "?",005,003,002,354,018,011,005
900 DATA "*",069,041,017,512,017,041,069
910 DATA "=",041,041,041,041,041,041,041
920 DATA "!",001,001,384,001,001,001,001
930 DATA ".",001,001,129,449,129,001,001
940 END
```
{% endraw %}

## BARIC.BAS

{% raw %}
```bas
2 GOSUB 5000
5 S=1000-S*100
10 CLS:PRINT CHR$(201);:PRINT STRING$(78,205);:PRINT CHR$(187);:FOR X=2 TO 23:LOCATE X,1:PRINT CHR$(186);:NEXT:FOR X=2 TO 23:LOCATE X,80:PRINT CHR$(186);:NEXT:PRINT CHR$(200);:PRINT STRING$(78,205);:PRINT CHR$(188);
20 X1=13:Y1=30:X2=13:Y2=50:A$=STRING$(2,219):B$=STRING$(2,176):LOCATE X1,Y1:PRINT A$:LOCATE X2,Y2:PRINT B$;:Z$="a":Y$="'":POKE 26,PEEK(28)
30 X$=INKEY$:IF X$<>"" THEN IF X$="w" OR X$="a" OR X$="s" OR X$="z" THEN Z$=X$ ELSE IF X$="[" OR X$="'" OR X$=";" OR X$="/" THEN Y$=X$
35 FOR V=1 TO S:NEXT
40 IF Z$="w" THEN X1=X1-1:GOSUB 1000:GOSUB 200
50 IF Z$="z" THEN X1=X1+1:GOSUB 1000:GOSUB 200
60 IF Z$="a" THEN Y1=Y1-2:GOSUB 1000:GOSUB 200
70 IF Z$="s" THEN Y1=Y1+2:GOSUB 1000:GOSUB 200
80 IF Y$="[" THEN X2=X2-1:GOSUB 1500:GOSUB 300
90 IF Y$="/" THEN X2=X2+1:GOSUB 1500:GOSUB 300
100 IF Y$=";" THEN Y2=Y2-2:GOSUB 1500:GOSUB 300
110 IF Y$="'" THEN Y2=Y2+2:GOSUB 1500:GOSUB 300
120 GOTO 30
200 LOCATE X1,Y1:PRINT A$;:RETURN
300 LOCATE X2,Y2:PRINT B$;:RETURN
1000 IF Y1<>0 THEN IF SCREEN(X1,Y1)=32 THEN RETURN
1010 LOCATE X1,Y1:IF Y1=0 THEN LOCATE X1,Y1+1 ELSE IF Y1=80 THEN LOCATE X1,Y1-1
1020 PRINT STRING$(2,1);:LOCATE 25,5:PRINT "crash !!! by -> ██";:TWO=TWO+1:GOTO 10000
1500 IF Y2<>0 THEN IF SCREEN(X2,Y2)=32 THEN RETURN
1510 LOCATE X2,Y2:IF Y2=0 THEN LOCATE X2,Y2+1 ELSE IF Y2=80 THEN LOCATE X2,Y2-1
1520 PRINT STRING$(2,2);:LOCATE 25,5:PRINT "crash !!! by  -> ░░";:ONE= ONE+1:GOTO 10000
5000 CLS:KEY OFF:DEF SEG=&H40
5010 PRINT "█▀▀▀▀▄      ██     █▀▀▀▀▀▀▄   ▀▀▀█▀▀▀  █▀▀▀▀▀▀▀     ██     █▀▀▀▀▄   █▀▀▀▀▀"
5020 PRINT "█     █    █  █    █      █      █     █           █  █    █     █  █"
5030 PRINT "█    ▄▀   █    █   █▄▄▄▄▄▄▀      █     █          █    █   █     █  █"
5040 PRINT "█▀▀▀▀▄   █▄▄▄▄▄▄█  █    █        █     █         █▄▄▄▄▄▄█  █     █  █▀▀▀▀▀"
5050 PRINT "█     █  █      █  █     █       █     █         █      █  █     █  █"
5060 PRINT "█▄▄▄▄▀   █      █  █      █ R ▄▄▄█▄▄▄  █▄▄▄▄▄▄▄  █      █  █▄▄▄▄▀   █▄▄▄▄▄"
5070 PRINT:PRINT
5080 PRINT "                        **********************"
5090 PRINT "                        *     Written by     *"
6000 PRINT "                        *     Yosef Gold     *"
6010 PRINT "                        **********************"
6070 PRINT:PRINT
6080 PRINT " This is a two person game. The object is to moke your line around the screen    and to set up a barricade for the other person.  You can't hit the sides of the  screen, your own line or the oponents barricade"
6090 PRINT:PRINT "      left player => ██                        right player => ░░"
6100 PRINT "         w  -> up                                 [ -> up"
6110 PRINT "         a  -> left                               ; -> left"
6120 PRINT "         s -> right                               ' -> right"
6130 PRINT "         z -> down                                / -> down"
6135 PRINT "                    each game is eleven rounds";
6140 LOCATE 25,1:INPUT "enter speed (1-10)";S:IF S<1 OR S>10 THEN LOCATE 25,1:GOTO 6140
7000 RETURN
10000 LOCATE 25,35:PRINT "██ has "ONE" points.    ░░ has "TWO"points.";:FOR V= 1 TO 4000:NEXT:IF ONE+TWO<11 THEN 10 ELSE CLS:KEY OFF:IF ONE>TWO THEN PRINT "player ██ HAS WON" ELSE PRINT "PLAYER ░░ HAS WON":PRINT:PRINT:PRINT
10010 PRINT:PRINT "player ██ won "ONE" games":PRINT "player ░░ won "TWO" games"
10020 LOCATE 20,1:PRINT "do you want to play again?";
10030 A$=INKEY$:IF A$="" THEN 10030 ELSE IF A$="y" THEN RUN
```
{% endraw %}

## CATALOG.TXT

{% raw %}
```
ANIMAL  .BAS - Guess the animal game.  
ANIMAL  .GME - Sample data file of animals for ANIMAL.BAS.
BANNER  .BAS - Make large banners on the screen.
LBANNER .BAS - Make large banners on the printer.
BARIC   .BAS - Two person Tron like game.
HAMURABI.BAS - Be king for a ten year term (game).
PICTURE .BAS+- 3-D graphic demo (requires color/graphics).
PINWHEEL.BAS - Animated program listing.
KBD_FIX .BAS - Creates KBD_FIX.COM.
KBD_FIX .COM - Increase keyboard buffer to 159 characters.
DAY     .PRG - dbase II program to find day of week.
DATE    .PRG - dbase II program to set date from system date.
TIME    .PRG - dbase II program to get system time.           
COMSTRIP.PRG - dbase II program to strip comments from other dbase II programs.
GEMINI  .ABS - Fix to use DOS 2.0 graphic dump on Gemini printer.
CATALOG .TXT - List of files on this disk.
```
{% endraw %}

## CRC.TXT

{% raw %}
```
PC-SIG Disk No. #128, version v1 
 
The following is a list of the file checksums which should be produced by
the CRCK4 program on this disk.  If the CRC numbers do not match the following
list you may have a bad file.  To use type:  CRCK4 <filespec>
 
CRCK4 output for this disk:
 

CRCK ver 4.2B (MS DOS VERSION )
CTL-S pauses, CTL-C aborts

--> FILE:  BARIC   .BAS         CRC = C7 4D

--> FILE:  PINWHEEL.BAS         CRC = E7 43

--> FILE:  CATALOG .TXT         CRC = 8A C4

--> FILE:  HAMURABI.BAS         CRC = D0 4D

--> FILE:  ANIMAL  .BAS         CRC = 9B 08

--> FILE:  PICTURE .BAS         CRC = 89 C5

--> FILE:  BANNER  .BAS         CRC = 10 36

--> FILE:  KBD_FIX .BAS         CRC = 07 90

--> FILE:  LBANNER .BAS         CRC = 1A 33

--> FILE:  KBD_FIX .COM         CRC = 56 25

--> FILE:  DAY     .PRG         CRC = 97 20

--> FILE:  DATE    .PRG         CRC = E8 B4

--> FILE:  TIME    .PRG         CRC = A7 83

--> FILE:  COMSTRIP.PRG         CRC = F0 33

--> FILE:  ANIMAL  .GME         CRC = 57 C3

--> FILE:  GEMINI  .ABS         CRC = 23 46

--> FILE:  DBS     .PRT         CRC = 50 DB

--> FILE:  README  .            CRC = 00 00

--> FILE:  DBS     .EXE         CRC = 62 DF

--> FILE:  CRCK4   .COM         CRC = BD 22

 ---------------------> SUM OF CRCS = B9 FB

DONE 
 
These and other Public Domain and user-supported programs from:
 
PC Software Interest Group (PC-SIG)
1125 Stewart Ct  Suite G
Sunnyvale, CA 94086
(408) 730-9291
```
{% endraw %}

## HAMURABI.BAS

{% raw %}
```bas
80 PRINT "Try your hand at governing ancient Sumaria"
85 PRINT "successfully for a 10 year term of office."
90 RANDOMIZE(VAL(MID$(TIME$,7,2))):D1=0:P=95:P1=0:Z=0:S=2800:H=3000:E=H-S:Y=3:A=H/Y:I=5:Q=1
210 D=0
215 PRINT:PRINT:PRINT "HAMURABI: I BEG TO REPORT TO YOU ,":Z=Z+1
217 PRINT "IN YEAR"Z","D"PEOPLE STARVED,"I"CAME TO THE CITY."
218 P=P+I
227 IF Q>0 THEN 230
228 P=INT(P/2)
229 PRINT "A HORRIBLE PLAGUE STRUCK ! HALF THE PEOPLE DIED."
230 PRINT "THE POPULATION IS NOW "P
232 PRINT "THE CITY OWNES "A" ACRES."
235 PRINT "YOU HARVESTED "Y" BUSHELS PER ACRE."
250 PRINT "RATS ATE " E" BUSHELS."
260 PRINT "YOU NOW HAVE "S"BUSHELS IN STORE.":PRINT
270 IF Z=11 THEN 860
310 C=INT(10*RND(0)):Y=C+17
312 PRINT "LAND IS TRADING AT "Y "BUSHELS PER ACRE."
320 PRINT "HOW MANY ACRES DO YOU WISH TO BUY";
321 INPUT Q:IF Q<0 THEN 850
322 IF Y*Q<=S THEN 330
323 GOSUB 710
324 GOTO 320
330 IF Q=0 THEN 340
331 A=A+Q:S=S-Y*Q:C=0
334 GOTO 400
335 RANDOMIZE(VAL(MID$(TIME$,7,2)))
340 PRINT "HOW MANY ACRES DO YOU WISH TO SELL";
341 INPUT Q:IF Q<0 THEN 850
342 IF Q<A THEN 350
343 GOSUB 720
344 GOTO 340
350 A=A-Q:S=S+Y*Q:C=0
400 PRINT
410 PRINT "HOW MANY BUSHELS DO YOU WISH TO FEED YOUR PEOPLE ";
411 INPUT Q
412 IF Q<0 THEN 850
418 '
419 RANDOMIZE(VAL(MID$(TIME$,7,2)))
420 IF Q<=S THEN 430
421 GOSUB 710
422 GOTO 410
430 S=S-Q:C=1:PRINT
440 PRINT "HOW MANY ACRES DO YOU WISH TO PLANT WITH SEED"
441 INPUT D:IF D=11 THEN 511
442 IF D<0 THEN 850
444 '
445 IF D<=A THEN 450
446 GOSUB 720
447 GOTO 440
449 '
450 IF INT(D/2)<S THEN 455
452 GOSUB 710
453 GOTO 440
454 '
455 IF D<10*P THEN 510
460 PRINT "BUT YOU HAVE ONLY"P"PEOPLE TO TEND THE FIELDS. NOW THEN,"
470 GOTO 440
510 S=S-INT(D/2)
511 GOSUB 800
512 '
515 Y=C:H=D*Y:E=0
521 GOSUB 800
522 IF INT(C/2)<>C/2 THEN 530
523 '
525 E=INT(S/C)
530 S=S-E+H
531 GOSUB 800
532 '
533 I=INT(C*(20*A+S)/P/100+1)
539 '
540 C=INT(Q/20)
541 '
542 Q=INT(10*(2*RND(0)-0.3))
545 RANDOMIZE(VAL(MID$(TIME$,7,2)))
550 IF P<C THEN 210
551 '
552 D=P-C:IF D>0.45*P THEN 560
553 P1=((Z-1)*P1+D*100/P)/Z
555 P=C:D1=D1+D:GOTO 215
560  PRINT :PRINT "YOU STARVED "D" PEOPLE IN ONE YEAR!!!"
565 PRINT "DUE TO EXTREME MISMANAGEMENT YOU HAVE NOT ONLY"
566 PRINT "BEEN IMPEACHED AND THRWOWN OUT OF OFFICE BUT YOU HAVE"
567 PRINT "ALSO BEEN DECLARED A 'NATIONAL FINK' !!":GOTO 990
710 PRINT "HAMURABI: THINK AGAIN . YOU HAVE ONLY"
711 PRINT S" BUSHELS OF GRAIN. NOW THEN,"
712 RETURN
720 PRINT "HAMURABI: THINK AGAIN. YOU OWN ONLY "A" ACRES. NOW THEN,"
730 RETURN
800 C=INT(RND(0)*5)+1
801 RETURN
850 PRINT "HAMURABI: I CANNOT DO WHAT YOU WISH."
855 PRINT "GET YOURSELF ANOTHER STEWARD !!!!!!!!!!!!"
857 GOTO 990
860 PRINT "IN YOUR 10 YEAR TERM OF OFFICE "P1" PERCENT OF THE "
862 PRINT "POPULATION STARVED PER YEAR ON AVERAGE, I.E., A TOTAL OF "
865 PRINT D1" PEOPLE DIED!!":L=A/P
870 PRINT "YOU STARTED WITH 10 ACRES PER PERSON AND ENDED WITH"
875 PRINT L" ACRES PER PERSON.":PRINT
880 IF P1>33 THEN 565
885 IF L<7 THEN 565
890 IF P1>10 THEN 940
892 IF L<9 THEN 940
895 IF P1>3 THEN 960
896 IF L<10 THEN 960
900 PRINT "A FANTASTIC PERFORMANCE!!! CHARLEMANGE, DISRAELI , AND"
905 PRINT "JEFFERSON COMBINED COULD NOT HAVE DONE BETTER!":GOTO 990
940 PRINT "YOUR HEAVY HANDED PERFORMANCE SMACKS OF NERO AND IVAN IV."
945 PRINT "THE PEOPLE (REMAINING) FIND YOU AN UNPLEASANT RULER, AND,"
950 PRINT "FRANKLY, HATE YOUR GUTS!":GOTO 990
960 PRINT "YOUR PERFORMANCE COULD HAVE BEEN SOMEWHAT BETTER, BUT "
965 PRINT "REALLY WASN'T TOO BAD AT ALL.  " INT(P*0.8*RND) " PEOPLE WOULD"
970 PRINT "DEARLY LIKE TO SEE YOU ASSASSINATED BUT WE ALL HAVE OUR"
975 PRINT "TRIVIAL PROBLEMS."
990 PRINT :FOR N=1 TO 10 : BEEP : NEXT
995 PRINT "SO LONG FOR NOW.":PRINT
999 END
```
{% endraw %}

## KBD_FIX.BAS

{% raw %}
```bas
10 DIM CHECK(48)
20 FOR I= 1 TO 48:CHECK(I)=0:NEXT
30 PRINT "checking";
40 FOR I=1 TO 48
50 FOR J= 1 TO 8
60 READ BYTE
70 CHECK(I)=CHECK(I) >= BYTE
80 NEXT J
90 PRINT ".";
100 NEXT I
110 PRINT
120 LINECHECK=0
130 FOR I= 1 TO 48
140 READ CHECK
150 LINECHECK=LINECHECK >= CHECK
160 IF CHECK(I)<>CHECK THEN PRINT "data in line";1000+10*(I-1);"may be bad."
170 NEXT I
180 IF LINECHECK<>228 THEN PRINT "data bad in lines 2010-2060."
190 OPEN "kbd_fix.com" AS #1 LEN=1
200 PRINT "writing..."
210 FIELD #1,1 AS BYTE$
220 RESTORE
230 FOR I= 1 TO 16
240 READ BYTE:LSET BYTE$=CHR$(BYTE):PUT #1
250 NEXT I
260 LSET BYTE$=CHR$(0)
270 FOR I=1 TO 312
280 PUT #1
290 NEXT I
300 FOR I=1 TO 368
310 READ BYTE:LSET BYTE$=CHR$(BYTE):PUT #1
320 NEXT I
330 CLOSE
340 PRINT "kbd_fix.com created"
350 END
1000 DATA 233,91,2,0,0,0,0,0
1010 DATA 0,0,0,15,1,15,1,0
1020 '312 bytes of 0's
1030 DATA 0,0,0,0,0,0,0,80
1040 DATA 83,81,156,250,187,30,0,228
1050 DATA 97,80,36,252,230,97,185,60
1060 DATA 0,226,254,12,2,230,97,185
1070 DATA 60,0,226,254,75,117,235,88
1080 DATA 230,97,157,89,91,88,195,187
1090 DATA 64,0,142,219,250,139,30,26
1100 DATA 0,59,30,28,0,117,29,187
1110 DATA 30,0,137,30,26,0,131,195
1120 DATA 2,137,30,28,0,140,203,142
1130 DATA 219,187,15,1,137,30,11,1
1140 DATA 137,30,13,1,251,195,30,86
1150 DATA 83,80,232,202,255,156,46,255
1160 DATA 30,3,1,187,64,0,142,219
1170 DATA 46,139,54,13,1,139,30,26
1180 DATA 0,131,195,2,131,251,62,114
1190 DATA 3,187,30,0,59,30,28,0
1200 DATA 116,38,139,7,46,137,4,131
1210 DATA 198,2,129,254,79,2,114,3
1220 DATA 190,15,1,46,59,54,11,1
1230 DATA 117,5,232,98,255,235,5,46
1240 DATA 137,54,13,1,137,30,26,0
1250 DATA 160,23,0,36,12,60,12,117
1260 DATA 8,46,161,13,1,46,163,11
1270 DATA 1,88,91,94,31,207,251,30
1280 DATA 83,232,99,255,140,203,142,219
1290 DATA 10,228,116,11,254,204,116,41
1300 DATA 254,204,116,54,91,31,207,251
1310 DATA 144,250,139,30,11,1,59,30
1320 DATA 13,1,116,243,139,7,131,195
1330 DATA 2,129,251,79,2,117,3,187
1340 DATA 15,1,137,30,11,1,91,31
1350 DATA 207,250,139,30,11,1,59,30
1360 DATA 13,1,139,7,251,91,31,202
1370 DATA 2,0,255,46,7,1,30,184
1380 DATA 0,0,142,216,250,161,36,0
1390 DATA 46,163,3,1,161,38,0,46
1400 DATA 163,5,1,199,6,36,0,166
1410 DATA 2,140,14,38,0,251,161,88
1420 DATA 0,46,163,7,1,161,90,0
1430 DATA 46,163,9,1,199,6,88,0
1440 DATA 14,3,140,14,90,0,184,64
1450 DATA 0,142,216,250,187,30,0,137
1460 DATA 30,26,0,199,7,0,0,131
1470 DATA 195,2,137,30,28,0,251,186
1480 DATA 94,3,205,39,0,0,0,0
2000 '
2010 DATA 176,0,80,37,235,44,173,56
2020 DATA 96,234,211,64,243,235,147,178
2030 DATA 16,118,159,254,135,153,197,62
2040 DATA 218,13,105,53,254,0,208,71
2050 DATA 248,215,143,245,115,41,38,228
2060 DATA 164,112,28,45,128,71,11,183
```
{% endraw %}

## LBANNER.BAS

{% raw %}
```bas
20 PRINT "WARNING !!!!! THE CAPS-LOCK KEY MUST BE ON !!! "
30 PRINT "-----------------------------------------------"
50 INPUT "horizontal size ";X
60 INPUT "vertical size ";Y
70 INPUT "centered (y/n)";L$
75 IF ASC(L$)>96 THEN PRINT "put caps lock key on":GOTO 70
80 G1=0: IF LEFT$(L$,1)="Y" THEN G1=1
90 PRINT "letter or word to make large letters from or 'ALL' to make the large letters out of same smaller one":INPUT M$
110 INPUT "statement to print";A$
120 INPUT; "set the page and press return to print";O$
140 FOR T= 1 TO LEN(A$)
150 P$=MID$(A$,T,1)
160 FOR O=1 TO 50
170 READ S$,S(1),S(2),S(3),S(4),S(5),S(6),S(7)
180 REM if p$=" " then 500
190 IF P$=S$ THEN 210
200 NEXT O
210 RESTORE
220 X$=M$
230 IF M$="ALL" THEN X$=S$
240 FOR U=1 TO 7
250 FOR K= 8 TO 0 STEP -1
260 IF 2^K<S(U) THEN 290
270 J(9-K)=0
280 GOTO 310
290 J(9-K)=1:S(U)=S(U)-2^K
300 IF S(U)=1 THEN 520
310 NEXT K
320 FOR T1=1 TO X
330 LPRINT TAB((63-4.5*Y)*G1/(LEN(X$))+1);
340 FOR B=1 TO F(U)
350 IF J(B)=0 THEN 380
360 FOR I=1 TO Y:LPRINT X$;:NEXT I
370 GOTO 420
380 FOR I=1 TO Y
390 FOR I1 =1 TO LEN(X$)
400 LPRINT " ";:NEXT I1
410 NEXT I
420 NEXT B
430 LPRINT " "
440 NEXT T1
450 NEXT U
460 FOR H=1 TO 2*X:LPRINT " ":NEXT H
470 NEXT T
480 FOR H=1 TO 75:LPRINT " ":NEXT H
490 END
500 FOR H=1 TO 7*X:LPRINT " ";:NEXT H
510 GOTO 470
520 F(U)=9-K:GOTO 320
530 DATA " ",000,000,000,000,000,000,000
540 DATA "A",505,037,035,034,035,037,505
550 DATA "B",512,274,274,274,274,274,239
560 DATA "C",125,131,258,258,258,131,069
570 DATA "D",512,258,258,258,258,131,125
580 DATA "E",512,274,274,274,274,258,258
590 DATA "F",512,018,018,018,018,002,002
600 DATA "G",125,131,258,258,290,163,101
610 DATA "H",512,017,017,017,017,017,512
620 DATA "I",258,258,258,512,258,258,258
630 DATA "J",065,129,257,257,257,129,128
635 DATA "K",512,017,017,041,069,131,259
640 DATA "L",512,257,257,257,257,257,257
650 DATA "M",512,007,013,125,013,007,512
660 DATA "N",512,007,009,017,033,193,512
670 DATA "O",125,131,258,258,258,131,125
680 DATA "P",512,018,018,018,018,018,015
690 DATA "Q",125,131,258,258,322,131,381
700 DATA "R",512,018,018,050,082,146,271
710 DATA "S",069,139,274,274,274,163,069
720 DATA "T",002,002,002,512,002,002,002
730 DATA "U",128,129,257,257,257,129,128
740 DATA "V",064,065,129,257,129,065,064
750 DATA "W",256,257,129,065,129,257,256
760 DATA "X",388,069,041,017,041,069,388
770 DATA "Y",008,009,017,481,017,009,008
780 DATA "Z",386,322,290,274,266,262,260
790 DATA "0",125,131,262,274,322,131,125
800 DATA "1",000,261,259,512,257,257,000
810 DATA "2",261,387,322,290,274,267,261
820 DATA "3",066,130,258,274,266,150,100
830 DATA "4",033,049,041,037,035,512,033
840 DATA "5",160,274,274,274,274,274,226
850 DATA "6",194,291,293,297,305,289,193
860 DATA "7",258,130,066,034,018,010,008
870 DATA "8",069,171,274,274,274,171,069
880 DATA "9",263,138,074,042,026,010,007
890 DATA "?",005,003,002,354,018,011,005
900 DATA "*",069,041,017,512,017,041,069
910 DATA "=",041,041,041,041,041,041,041
920 DATA "!",001,001,384,001,001,001,001
930 DATA ".",001,001,129,449,129,001,001
940 END
```
{% endraw %}

## PICTURE.BAS

{% raw %}
```bas
10 SCREEN 105:SCREEN ,,3,3:KEY OFF:CLS
20 P=300:Q=90:XP=250:XR=1.5*3.1415927#:YP=56:YR=1:ZP=64:XF=XR/XP:YF=YP/YR:ZF=XR/ZP:FOR ZI=-Q TO Q-1:IF ZI<-ZP OR ZI>ZP THEN GOTO 50
30 ZT=ZI*XP/ZP:ZZ=ZI:XL=INT(0.5+SQR(XP*XP-ZT*ZT)):FOR XI=-XL TO XL:XT=SQR(XI*XI+ZT*ZT)*XF:XX=XI:YY=SIN(SIN(XT)+0.4*SIN(3*XT))*YF
40 X1=XX+ZZ+P:Y1=-YY+ZZ+Q:PSET(X1,Y1),1:NEXT XI
50 NEXT ZI
```
{% endraw %}

## PINWHEEL.BAS

{% raw %}
```bas
1 CLEAR:CLS:DEF SEG=&H40:POKE 26,32:POKE 28,44:POKE 32,82:POKE 34,85:POKE 36,78:POKE 38,32:POKE 40,50:POKE 42,13:LIST
2 LOCATE 6,0:PRINT CHR$(76)+CHR$(73)+CHR$(83)+CHR$(84)+STRING$(2,32);
3 A$="I/-"+CHR$(92):FOR A=1 TO 48 : OUT 97,79: OUT 67,161: OUT 66,A: FOR C=1 TO A: NEXT:X$=MID$(A$,A-INT(A/4)*4+1,1):LOCATE 4,7:PRINT X$X$X$;:NEXT:GOTO 3
```
{% endraw %}

{% comment %}samples_end{% endcomment %}

### Directory of PC-SIG Library Disk #0128

     Volume in drive A has no label
     Directory of A:\

    ANIMAL   BAS      2432   1-01-80  12:35a
    ANIMAL   GME       640   1-01-80  12:23a
    BANNER   BAS      2816   1-01-80  12:07a
    BARIC    BAS      2816   1-01-80  12:01a
    CATALOG  TXT       896   1-01-80  12:02a
    COMSTRIP PRG       512   1-01-80   1:40a
    CRC      TXT      1522   1-01-80  12:07a
    CRCK4    COM      1536  10-21-82   5:50p
    DATE     PRG       512   1-01-80   1:27a
    DAY      PRG      1024   1-01-80   1:20a
    DBS      EXE     39936   7-13-86   2:39p
    GEMINI   ABS      1152   1-01-80   1:15a
    HAMURABI BAS      3328   1-01-80  12:03a
    KBD_FIX  BAS      2432   1-01-80  12:01a
    KBD_FIX  COM       768   1-01-80   1:23a
    LBANNER  BAS      2816   1-01-80  12:06a
    PICTURE  BAS       384   1-01-80  12:01a
    PINWHEEL BAS       384   1-01-80   1:44a
    READ     ME       1024   7-13-86   2:43p
    README              66   1-15-84   2:36p
    TIME     PRG       512   1-01-80   1:34a
           21 file(s)      67508 bytes
                           89600 bytes free
